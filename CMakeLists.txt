CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

PROJECT(6502_Emulator VERSION 1.0.0 DESCRIPTION "6502 Emulator" LANGUAGES CXX)
IF(APPLE)
  ENABLE_LANGUAGE(OBJC)
  ENABLE_LANGUAGE(OBJCXX)
ENDIF()

# Determine if thisproject is built as a subproject (using add_subdirectory)
# or if it is the master project.
IF (NOT DEFINED MASTER_PROJECT)
  SET(MASTER_PROJECT OFF)
  IF (CMAKE_CURRENT_SOURCE_DIR STREQUAL CMAKE_SOURCE_DIR)
    SET(MASTER_PROJECT ON)
    MESSAGE(STATUS "CMake version: ${CMAKE_VERSION}")
  ENDIF ()
ENDIF ()

# Only affects CMake
OPTION(VERBOSE "Enable verbosity" OFF)

#
# Shared Compiler Definitions
#
OPTION(DEBUG "Enable debug (extensive logging)" OFF)
#OPTION(DECIMAL_MODE "Enable decimal mode" ON)
OPTION(WITH_LOGGING "Enable logging" ${DEBUG})
OPTION(WITH_ILLEGAL "Enable illegal instructions" ON)
OPTION(EMULATE65C02 "Emulate 65C02 (clears decimal flag on interrupt)" ON)
SET(LOGFILE "Processor.log" CACHE STRING "Filename to log to")

SET(PROJECT_COMPILE_DEFINITIONS)
IF(DEBUG)
  LIST(APPEND PROJECT_COMPILE_DEFINITIONS DEBUG=${DEBUG})
ENDIF()
#IF(DECIMAL_MODE)
#  LIST(APPEND PROJECT_COMPILE_DEFINITIONS DECIMAL_MODE=${DECIMAL_MODE})
#ENDIF()
IF(WITH_ILLEGAL)
  LIST(APPEND PROJECT_COMPILE_DEFINITIONS ILLEGAL=${WITH_ILLEGAL})
ENDIF()
IF(WITH_LOGGING)
  LIST(APPEND PROJECT_COMPILE_DEFINITIONS LOGMODE=${WITH_LOGGING})
ENDIF()
IF(EMULATE65C02)
  LIST(APPEND PROJECT_COMPILE_DEFINITIONS EMULATE65C02=${EMULATE65C02})
ENDIF()

IF(
  DEFINED LOGFILE
  AND NOT LOGFILE STREQUAL ""
  AND NOT LOGFILE STREQUAL "Processor.log"
)
  LIST(APPEND PROJECT_COMPILE_DEFINITIONS LOGFILE=${LOGFILE})
ENDIF()
#
# // Shared Compiler Definitions
#

IF (VERBOSE)
  SET(CMAKE_VERBOSE_MAKEFILE ON)
  SET(CMAKE_RULE_MESSAGES OFF)
ENDIF()

SET(PROJECT_COMPILE_FLAGS)
SET(PROJECT_LINK_FLAGS)
IF (CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
  IF(CMAKE_BUILD_TYPE STREQUAL "Debug")
    LIST(APPEND PROJECT_COMPILE_FLAGS "--coverage" "-g" "-O0" "-fno-inline" "-fno-inline-small-functions" "-fno-default-inline")
    LIST(APPEND PROJECT_LINK_FLAGS "--coverage")
  ENDIF()
ELSEIF (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
  IF(CMAKE_BUILD_TYPE STREQUAL "Debug")
    LIST(APPEND PROJECT_COMPILE_FLAGS "--coverage")
    LIST(APPEND PROJECT_LINK_FLAGS "--coverage")
  ENDIF()
ENDIF()

LIST(APPEND CMAKE_MODULE_PATH
  ${PROJECT_SOURCE_DIR}/CMake
  ${CMAKE_BINARY_DIR}
  ${CMAKE_MODULE_PATH}
)

LIST(APPEND CMAKE_PREFIX_PATH
  ${PROJECT_SOURCE_DIR}/
  ${CMAKE_BINARY_DIR}
  ${CMAKE_MODULE_PATH}
)

IF(LINUX OR APPLE)
  LIST(APPEND CMAKE_PREFIX_PATH "/usr/local/include")
ENDIF()

ADD_SUBDIRECTORY(${PROJECT_SOURCE_DIR}/includes)
ADD_SUBDIRECTORY(${PROJECT_SOURCE_DIR}/Processor)

ADD_SUBDIRECTORY(${PROJECT_SOURCE_DIR}/ProcessorTests)
ADD_SUBDIRECTORY(${PROJECT_SOURCE_DIR}/UserInterfaces)
