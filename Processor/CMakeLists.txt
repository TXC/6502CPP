CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

PROJECT(
  6502_Processor
  VERSION 1.0.0
  DESCRIPTION "6502 Processor"
  LANGUAGES CXX
)

SET(APP_NAME Processor)

OPTION(${APP_NAME}_INSTALL "Generate installation target" ON)

GET_FILENAME_COMPONENT(PARENT_DIR ../ ABSOLUTE)

INCLUDE(Common)

ADD_LIBRARY(${APP_NAME})
ADD_LIBRARY(${APP_NAME}::${APP_NAME} ALIAS ${APP_NAME})

TARGET_SOURCES(${APP_NAME} INTERFACE
  FILE_SET HEADERS
  FILES Bus.hpp
        Exceptions.hpp
        Executioner.hpp
        Formatters.hpp
        Instructions.hpp
        Logger.hpp
        Processor.hpp
        Singleton.hpp
        Types.hpp
)

TARGET_SOURCES(${APP_NAME} PRIVATE
        Bus.cpp
        Executioner.cpp
        Logger.cpp
        Instructions.cpp
        Processor.cpp
)

TARGET_COMPILE_FEATURES(${APP_NAME} PUBLIC cxx_std_20)

TARGET_INCLUDE_DIRECTORIES(${APP_NAME} PUBLIC
  "$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}>"
  "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>"
)


#COMMON_LANGUAGE_PARAMETERS()
COMMON_SET_PROJECT_FLAGS()

# BEGIN HEADER ONLY
ADD_LIBRARY(${APP_NAME}-header-only INTERFACE)
ADD_LIBRARY(${APP_NAME}::${APP_NAME}-header-only ALIAS ${APP_NAME}-header-only)

TARGET_COMPILE_DEFINITIONS(${APP_NAME}-header-only INTERFACE HEADER_ONLY=1 DECIMAL_MODE=1)
TARGET_COMPILE_FEATURES(${APP_NAME}-header-only INTERFACE cxx_std_20)

TARGET_INCLUDE_DIRECTORIES(${APP_NAME}-header-only INTERFACE
  "$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}>"
  "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>"
)
# END HEADER ONLY

# BEGIN DEPENDENCIES
FIND_PACKAGE(fmt REQUIRED CONFIG)
IF(fmt_FOUND)
  TARGET_LINK_LIBRARIES(
    ${APP_NAME}
    PUBLIC fmt::fmt-header-only
  )
  TARGET_LINK_LIBRARIES(
    ${APP_NAME}-header-only
    INTERFACE fmt::fmt-header-only
  )
ENDIF()

FIND_PACKAGE(spdlog REQUIRED)
IF(spdlog_FOUND)
  TARGET_LINK_LIBRARIES(
    ${APP_NAME}
    PUBLIC spdlog::spdlog_header_only
  )
  TARGET_LINK_LIBRARIES(
    ${APP_NAME}-header-only
    INTERFACE spdlog::spdlog_header_only
  )
ENDIF()
# END DEPENDENCIES

COMMON_CREATE_INSTALL(NAME Processor
  TARGETS Processor Processor-header-only
  INSTALL
)
